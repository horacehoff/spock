use std::str::FromStr;
use crate::Opcode;
use crate::parser::Expr;
use crate::display::format_parser_error;
use inline_colorization::*;
use crate::error_b;
use crate::Num;
use crate::is_float;
use internment::Intern;
use crate::ops::remove_priority;
use crate::ops::op_to_rpn;
use crate::display::format_parser_error_recovery;
use concat_string::concat_string;
use crate::display::format_expr_type;


grammar;

match {
    r#"[\s^a_z]*"# => { },
    r"//[^\n\r]*[\n\r]*" => { }, // `// comment`
} else {_}

Comma<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

Args<T>: Vec<T> = {
    "(" <args: Comma<T>> ")" => args
}

Block<T>: T = {
    "{" <o1: T> "}" => o1
}


pub File:Vec<Expr> = {
    <o: Function*> => o,
}

pub Function:Expr = {
    "fn" <o1:Var> <args: Args<Var>> <o3: Block<Code>> => {
        return Expr::FunctionDecl(Box::from([&[o1], args.as_slice()].concat()), o3);
    }
}

ElseIfBlock:Expr = {
    "else" "if" <o1: Expression> <o2: Block<Code>> => Expr::ElseIfBlock(Box::from(o1), o2)
}


InlineElseIfBlock:Expr = {
    "else" "if" <o1: Expression> <o2: Block<Expression>> => Expr::ElseIfBlock(Box::from(o1), Box::from([o2]))
}

ElseBlock:Expr = {
    "else" <o2: Block<Code>> => Expr::ElseBlock(o2)
}

InlineElseBlock:Expr = {
    "else" <o2: Block<Expression>> => Expr::ElseBlock(Box::from([o2]))
}

ConditionalBlock: Expr = {
    "if" <o1: Expression> <o2: Block<Code>> <o3: ElseIfBlock*> <o4: ElseBlock?> => {
        if o1 == Expr::Bool(true) {
            return Expr::EvalBlock(o2);
        }
        let mut code = o2.to_vec();
        for x in o3 {
            code.push(x);
        }
        if let Some(x) = o4 {
            code.push(x);
        }
        Expr::Condition(Box::from(o1), code.into_boxed_slice())
    }
}
InlineConditionalBlock: Expr = {
    "if" <o1: Expression> <o2: Block<Expression>> <o3: InlineElseIfBlock*> <o4: InlineElseBlock?> => {
        if o1 == Expr::Bool(true) {
            return o2;
        }
        let mut code = vec![o2];
        for x in o3 {
            code.push(x);
        }
        if let Some(x) = o4 {
            code.push(x);
        }
        Expr::Condition(Box::from(o1), code.into_boxed_slice())
    }
}


MatchBlock:Expr = {
    "match" <o1: Expression> "{" <o2: (Expression "=>" Block<Code>)+> <o3: ("_" "=>" Block<Code>)?> "}" => {
        let match_obj = o1;
        let first_match = o2.first().unwrap();

        let mut output_code:Vec<Expr> = first_match.2.to_vec();
        output_code.extend(o2.iter().skip(1).map(|x| Expr::ElseIfBlock(Box::from(Expr::Op(Box::from([match_obj.clone(), x.0.clone(), Expr::Opcode(Opcode::Eq)]))), x.2.clone())));

        if let Some(wildcard) = o3 {
            output_code.push(Expr::ElseBlock(wildcard.2.clone()))
        }

        Expr::Condition(Box::from(Expr::Op(Box::from([match_obj, first_match.0.clone(), Expr::Opcode(Opcode::Eq)]))),Box::from(output_code))
    }
}


WhileBlock: Expr = {
    "while" <o1: Expression> <o2: Block<Code>> => Expr::WhileBlock(Box::from(o1), o2)
}

ForLoop:Expr = {
    "for" <o1:Var> "in" <o2: Expression> <o3: Block<Code>> => {
        let mut output = vec![o2];
        output.extend(o3);
        Expr::ForLoop(Intern::from(o1),output.into_boxed_slice())
    }
}

pub Code: Box<[Expr]> = {
    <s: Statement*> => s.into_boxed_slice()
}

// TO-DO
pub Import:Expr = {
    <path: r"import [a-zA-Z_.\/-]*"> => {
        Expr::Import(String::from(path.trim_start_matches("import").trim()))
    },
    ! => {
        error_b!(format_parser_error_recovery(<>, "IMPORT STATEMENT"));
    }
}

EvalBlock:Expr = {
    Block<Code> => {
        Expr::EvalBlock(<>)
    }
}

LoopBlock:Expr = {
    "loop" <o1: Block<Code>> => {
        Expr::LoopBlock(o1)
    }
}

Statement: Expr = {
    ConditionalBlock => <>,
    WhileBlock => <>,
    ForLoop => <>,
    MatchBlock => <>,
    EvalBlock => <>,
    Function => <>,
    LoopBlock => <>,
    <l: Line> ";" => l,
}

Line: Expr = {
    VarDeclare => <>,
    VarAssign => <>,
    OpVarAssign => <>,
    ReturnLn => <>,
    BreakLoop => <>,
    ContinueLoop => <>,
    Expression  => <>,
}

ReturnLn: Expr = {
    "return" <e: Expression?> => Expr::ReturnVal(Box::new(e))
}

BreakLoop:Expr = {
    "break" => Expr::Break
}

ContinueLoop:Expr = {
    "continue" => Expr::Continue
}

VarDeclare: Expr = {
    "let" <v: Var> "=" <e: Expression> => Expr::VarDeclare(Intern::from(v), Box::from(e)),
}

VarAssign: Expr = {
    <v: Var> "=" <e: Expression> => Expr::VarAssign(Intern::from(v), Box::from(e)),
    <t: Term> <f: ("[" Expression "]")+> "=" <e: Expression> => Expr::ArrayModify(Box::from(t), f.iter().map(|x| x.1.clone()).collect::<Vec<Expr>>().into_boxed_slice(), Box::from(e)),
    ! => {
        error_b!(format_parser_error_recovery(<>, "VARIABLE ASSIGNMENT"));
    }
}

OpVarAssign: Expr = {
    <v: Var> "+=" <e: Expression> => Expr::VarAssign(Intern::from_ref(&v), Box::from(Expr::Op(Box::from([Expr::Var(Intern::from(v)),e, Expr::Opcode(Opcode::Add)])))),
    <v: Var> "-=" <e: Expression> => Expr::VarAssign(Intern::from_ref(&v), Box::from(Expr::Op(Box::from([Expr::Var(Intern::from(v)),e, Expr::Opcode(Opcode::Sub)])))),
    <v: Var> "*=" <e: Expression> => Expr::VarAssign(Intern::from_ref(&v), Box::from(Expr::Op(Box::from([Expr::Var(Intern::from(v)),e,Expr::Opcode(Opcode::Mul)])))),
    <v: Var> "/=" <e: Expression> => Expr::VarAssign(Intern::from_ref(&v), Box::from(Expr::Op(Box::from([Expr::Var(Intern::from(v)),e,Expr::Opcode(Opcode::Div)])))),
    <v: Var> "%=" <e: Expression> => Expr::VarAssign(Intern::from_ref(&v), Box::from(Expr::Op(Box::from([Expr::Var(Intern::from(v)),e,Expr::Opcode(Opcode::Mod)])))),
    <v: Var> "^=" <e: Expression> => Expr::VarAssign(Intern::from_ref(&v), Box::from(Expr::Op(Box::from([Expr::Var(Intern::from(v)),e,Expr::Opcode(Opcode::Pow)])))),
}

IndexAccess: Expr = {
    <t: Term> <f: ("[" Expression "]")+> => {
        Expr::GetIndex(Box::from(t), f.iter().map(|x| x.1.clone()).collect::<Vec<Expr>>().into_boxed_slice())
    }
}



Expression: Expr = {
    <o:Operation> => o,
    <t: Term> => t,
    <i: IndexAccess> => i,
}

Operation: Expr = {
    "-" <t: Term> => {
        if let Expr::Num(x) = t {
            return Expr::Num(-x);
        }
        return Expr::Op(Box::from([Expr::Num(0.0 as Num), t, Expr::Opcode(Opcode::Neg)]));
    },
    <o1: Term> <o3: (<Op> <Term>)+> => {
        let mut output_op:Vec<Expr> = vec![o1];
        for x in o3 {
            output_op.push(Expr::Opcode(x.0));
            output_op.extend(remove_priority(x.1));
        }
        output_op = op_to_rpn(output_op);


        // constant folding
        if output_op.iter().any(|x| !matches!(x, Expr::Num(_) | Expr::Bool(_) | Expr::String(_) | Expr::Opcode(_))) {
            Expr::Op(output_op.into_boxed_slice())
        } else {
            let mut output_num:Expr = Expr::Num(0.0 as Num);
            let mut stack:Vec<Expr> = Vec::new();
            for x in output_op {
                if let Expr::Opcode(op) = x {
                    let last = stack.pop().unwrap();
                    let first = {
                        if let Some(num) = stack.pop() {
                            num
                        } else {
                            output_num
                        }
                    };
                    output_num = match op {
                        Opcode::Add => {
                            match (first,last) {
                            (Expr::Num(x),Expr::Num(y)) => Expr::Num(x+y),
                            (Expr::String(x),Expr::String(y)) => Expr::String(concat_string!(x,y)),
                            (x,y) => {error_b!(format_args!(
                                  "UNSUPPORTED OPERATION: {} + {}",
                                  format_expr_type(x),
                                  format_expr_type(y)
                              ));}
                            }
                        },
                        Opcode::Mul => {
                            match (first,last) {
                            (Expr::Num(x),Expr::Num(y)) => Expr::Num(x*y),
                            (x,y) => {error_b!(format_args!(
                                  "UNSUPPORTED OPERATION: {} * {}",
                                  format_expr_type(x),
                                  format_expr_type(y)
                              ));}
                            }
                        },
                        Opcode::Div => {
                            match (first,last) {
                            (Expr::Num(x),Expr::Num(y)) => Expr::Num(x/y),
                            (x,y) => {error_b!(format_args!(
                                  "UNSUPPORTED OPERATION: {} / {}",
                                  format_expr_type(x),
                                  format_expr_type(y)
                              ));}
                            }
                        },
                        Opcode::Sub => {
                            match (first,last) {
                            (Expr::Num(x),Expr::Num(y)) => Expr::Num(x-y),
                            (x,y) => {error_b!(format_args!(
                                  "UNSUPPORTED OPERATION: {} - {}",
                                  format_expr_type(x),
                                  format_expr_type(y)
                              ));}
                            }
                        },
                        Opcode::Mod => {
                            match (first,last) {
                            (Expr::Num(x),Expr::Num(y)) => Expr::Num(x%y),
                            (x,y) => {error_b!(format_args!(
                                  "UNSUPPORTED OPERATION: {} % {}",
                                  format_expr_type(x),
                                  format_expr_type(y)
                              ));}
                            }
                        },
                        Opcode::Pow => {
                            match (first,last) {
                            (Expr::Num(x),Expr::Num(y)) => Expr::Num(is_float!(x.powf(y),x.pow(y as u32))),
                            (x,y) => {error_b!(format_args!(
                                  "UNSUPPORTED OPERATION: {} ^ {}",
                                  format_expr_type(x),
                                  format_expr_type(y)
                              ));}
                            }
                        },
                        Opcode::Eq => {
                            Expr::Bool(first == last)
                        },
                        Opcode::NotEq => {
                            Expr::Bool(first != last)
                        },
                        Opcode::Sup => {
                            match (first,last) {
                            (Expr::Num(x),Expr::Num(y)) => Expr::Bool(x > y),
                            (x,y) => {error_b!(format_args!(
                                  "UNSUPPORTED OPERATION: {} > {}",
                                  format_expr_type(x),
                                  format_expr_type(y)
                              ));}
                            }
                        },
                        Opcode::SupEq => {
                            match (first,last) {
                            (Expr::Num(x),Expr::Num(y)) => Expr::Bool(x >= y),
                            (x,y) => {error_b!(format_args!(
                                  "UNSUPPORTED OPERATION: {} >= {}",
                                  format_expr_type(x),
                                  format_expr_type(y)
                              ));}
                            }
                        },
                        Opcode::Inf => {
                            match (first,last) {
                            (Expr::Num(x),Expr::Num(y)) => Expr::Bool(x < y),
                            (x,y) => {error_b!(format_args!(
                                  "UNSUPPORTED OPERATION: {} < {}",
                                  format_expr_type(x),
                                  format_expr_type(y)
                              ));}
                            }
                        },
                        Opcode::InfEq => {
                            match (first,last) {
                            (Expr::Num(x),Expr::Num(y)) => Expr::Bool(x <= y),
                            (x,y) => {error_b!(format_args!(
                                  "UNSUPPORTED OPERATION: {} <= {}",
                                  format_expr_type(x),
                                  format_expr_type(y)
                              ));}
                            }
                        },
                        Opcode::BoolAnd => {
                            match (first,last) {
                            (Expr::Bool(x),Expr::Bool(y)) => Expr::Bool(x && y),
                            (x,y) => {error_b!(format_args!(
                                  "UNSUPPORTED OPERATION: {} && {}",
                                  format_expr_type(x),
                                  format_expr_type(y)
                              ));}
                            }
                        },
                        Opcode::BoolOr => {
                            match (first,last) {
                            (Expr::Bool(x),Expr::Bool(y)) => Expr::Bool(x || y),
                            (x,y) => {error_b!(format_args!(
                                  "UNSUPPORTED OPERATION: {} || {}",
                                  format_expr_type(x),
                                  format_expr_type(y)
                              ));}
                            }
                        }
                        _ => unreachable!()
                        }
                } else {
                    stack.push(x);
                }
            }
            output_num
        }
    }
}

Term: Expr = {
    "(" <o2: Expression> ")" => Expr::Priority(Box::new(o2)),
    <t: Num> => Expr::Num(t),
    <b: Bool> => Expr::Bool(b),
    <s: String> => Expr::String(s),
    <v: Var> => Expr::Var(Intern::from(v)),
    <a: Array> => <>,
    <f: FunctionCall> => <>,
    //<o: ObjFunctionCall> => <>,
    <c: InlineConditionalBlock> => <>,
    //<i: ArrayIndex> => <>,
}

ItemBefore<T,S>: T = {
    <l: T> S => l
}
ItemAfter<S,T>: T = {
    S <l: T> => l
}

FunctionCall: Expr = {
    <namespace: ItemBefore<Var, "::">+?> <o1: Var> <args: Args<Expression>> => {
        Expr::FunctionCall(args.into_boxed_slice(), {
            if let Some(ns) = namespace {
                Box::from([ns.as_slice(), &[o1]].concat())
            } else {
                Box::from(vec![String::from(o1)])
            }
        })
    }
}

ObjFunctionCall:Expr = {
    <t: Term> <o: ItemAfter<".", FunctionCall>+> => {
        let mut func_calls = Vec::new();
        for x in o { if let Expr::FunctionCall(y,x) = x { func_calls.push((x.last().unwrap().to_string(),y)) } }
        Expr::ObjFunctionCall(Box::from(t), Box::from(func_calls))
    }
}

Array:Expr = {
    "[" <arr: Comma<Expression>> "]" => {
        Expr::Array(arr.into_boxed_slice())
    }
}

Num: Num = {
    <n: r"([0-9]*[.])?[0-9]+"> => Num::from_str(n).unwrap_or_else(|_| {error_b!(format_args!("Invalid number: '{color_red}{n}{color_reset}'"));})
}

Bool: bool = {
    "true" => true,
    "false" => false,
}

String: String = {
    <s: r#"\"(?:\"\"|[^\"])*\""#> => String::from(s.trim_matches('"'))
}

Var: String = {
    <v: r#"[a-zA-Z_]*"#> => String::from(v),
}

Op: Opcode = {
    "*" => Opcode::Mul,
    "+" => Opcode::Add,
    "/" => Opcode::Div,
    "-" => Opcode::Sub,
    "%" => Opcode::Mod,
    "^" => Opcode::Pow,
    "==" => Opcode::Eq,
    "!="  =>  Opcode::NotEq,
    ">"  =>  Opcode::Sup,
     ">="  => Opcode::SupEq,
     "<" =>   Opcode::Inf,
     "<="  => Opcode::InfEq,
     "&&"  => Opcode::BoolAnd,
     "||"  => Opcode::BoolOr,
}
