use std::str::FromStr;
use crate::parser::Expr;
use crate::display::format_parser_error;
use inline_colorization::*;
use crate::error_b;
use crate::Num;
use crate::is_float;
use internment::Intern;
use crate::display::format_parser_error_recovery;
use concat_string::concat_string;
use crate::display::format_expr_type;


grammar;

match {
    r#"[\s^a_z]*"# => { },
    r"//[^\n\r]*[\n\r]*" => { }, // `// comment`
} else {_}

Comma<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

Args<T>: Vec<T> = {
    "(" <args: Comma<T>> ")" => args
}

Block<T>: T = {
    "{" <o1: T> "}" => o1
}


pub File:Vec<Expr> = {
    <o: Function*> => o,
}

pub Function:Expr = {
    "fn" <o1:Var> <args: Args<Var>> <o3: Block<Code>> => {
        return Expr::FunctionDecl(Box::from([&[o1], args.as_slice()].concat()), o3);
    }
}

ElseIfBlock:Expr = {
    "else" "if" <o1: Expression> <o2: Block<Code>> => Expr::ElseIfBlock(Box::from(o1), o2)
}


InlineElseIfBlock:Expr = {
    "else" "if" <o1: Expression> <o2: Block<Expression>> => Expr::ElseIfBlock(Box::from(o1), Box::from([o2]))
}

ElseBlock:Expr = {
    "else" <o2: Block<Code>> => Expr::ElseBlock(o2)
}

InlineElseBlock:Expr = {
    "else" <o2: Block<Expression>> => Expr::ElseBlock(Box::from([o2]))
}

ConditionalBlock: Expr = {
    "if" <o1: Expression> <o2: Block<Code>> <o3: ElseIfBlock*> <o4: ElseBlock?> => {
        if o1 == Expr::Bool(true) {
            return Expr::EvalBlock(o2);
        }
        Expr::Condition(
            Box::from(o1),
            Box::from(
                [&(*o2),
                o3.as_slice(),
                match o4 {
                    Some(ref value) => std::slice::from_ref(value),
                    None => &[],
                }]
            .concat()
        ))
    }
}
InlineConditionalBlock: Expr = {
    "if" <o1: Expression> <o2: Block<Expression>> <o3: InlineElseIfBlock*> <o4: InlineElseBlock?> => {
        if o1 == Expr::Bool(true) {
            return o2;
        }
        Expr::Condition(
            Box::from(o1),
            Box::from(
                [
                std::slice::from_ref(&o2),
                o3.as_slice(),
                match o4 {
                    Some(ref value) => std::slice::from_ref(value),
                    None => &[],
                }]
            .concat()
        ))
    }
}


MatchBlock:Expr = {
    "match" <o1: Expression> "{" <o2: (Expression "=>" Block<Code>)+> <o3: ("_" "=>" Block<Code>)?> "}" => {
        let match_obj = o1;
        let first_match = o2.first().unwrap();

        let mut output_code:Vec<Expr> = first_match.2.to_vec();
        output_code.extend(o2.iter().skip(1).map(|x| Expr::ElseIfBlock(
            Box::from(
                Expr::Eq(
                    Box::from(match_obj.clone()),
                    Box::from(x.0.clone())
                )
            ),
            x.2.clone()
        )));

        if let Some(wildcard) = o3 {
            output_code.push(Expr::ElseBlock(wildcard.2.clone()))
        }

        Expr::Condition(Box::from(Expr::Eq(Box::from(match_obj), Box::from(first_match.0.clone()))),Box::from(output_code))
    }
}


WhileBlock: Expr = {
    "while" <o1: Expression> <o2: Block<Code>> => Expr::WhileBlock(Box::from(o1), o2)
}

ForLoop:Expr = {
    "for" <o1:Var> "in" <o2: Expression> <o3: Block<Code>> => {
        let mut output = vec![o2];
        output.extend(o3);
        Expr::ForLoop(Intern::from(o1),output.into_boxed_slice())
    }
}

pub Code: Box<[Expr]> = {
    <s: Statement*> => s.into_boxed_slice()
}

// TO-DO
pub Import:Expr = {
    <path: r"import [a-zA-Z_.\/-]*"> => {
        Expr::Import(String::from(path.trim_start_matches("import").trim()))
    },
    ! => {
        error_b!(format_parser_error_recovery(<>, "IMPORT STATEMENT"));
    }
}

EvalBlock:Expr = {
    Block<Code> => {
        Expr::EvalBlock(<>)
    }
}

LoopBlock:Expr = {
    "loop" <o1: Block<Code>> => {
        Expr::LoopBlock(o1)
    }
}

Statement: Expr = {
    ConditionalBlock => <>,
    WhileBlock => <>,
    ForLoop => <>,
    MatchBlock => <>,
    EvalBlock => <>,
    Function => <>,
    LoopBlock => <>,
    <l: Line> ";" => l,
}

Line: Expr = {
    VarDeclare => <>,
    VarAssign => <>,
    OpVarAssign => <>,
    ReturnLn => <>,
    BreakLoop => <>,
    ContinueLoop => <>,
    Expression  => <>,
}

ReturnLn: Expr = {
    "return" <e: Expression?> => Expr::ReturnVal(Box::new(e))
}

BreakLoop:Expr = {
    "break" => Expr::Break
}

ContinueLoop:Expr = {
    "continue" => Expr::Continue
}

VarDeclare: Expr = {
    "let" <v: Var> "=" <e: Expression> => Expr::VarDeclare(Intern::from(v), Box::from(e)),
}

VarAssign: Expr = {
    <v: Var> "=" <e: Expression> => Expr::VarAssign(Intern::from(v), Box::from(e)),
    <t: Term> <f: ("[" Expression "]")+> "=" <e: Expression> => Expr::ArrayModify(Box::from(t), f.iter().map(|x| x.1.clone()).collect::<Vec<Expr>>().into_boxed_slice(), Box::from(e)),
    ! => {
        error_b!(format_parser_error_recovery(<>, "VARIABLE ASSIGNMENT"));
    }
}

OpVarAssign: Expr = {
    <v: Var> "+=" <e: Expression> => Expr::VarAssign(Intern::from_ref(&v), Box::from(
    Expr::Add(
        Box::from(Expr::Var(Intern::from_ref(&v))),
        Box::from(e),
    ))),
    <v: Var> "-=" <e: Expression> => Expr::VarAssign(Intern::from_ref(&v), Box::from(
    Expr::Sub(
        Box::from(Expr::Var(Intern::from_ref(&v))),
        Box::from(e),
    ))),
    <v: Var> "*=" <e: Expression> => Expr::VarAssign(Intern::from_ref(&v), Box::from(
    Expr::Mul(
        Box::from(Expr::Var(Intern::from_ref(&v))),
        Box::from(e),
    ))),
    <v: Var> "/=" <e: Expression> => Expr::VarAssign(Intern::from_ref(&v), Box::from(
    Expr::Div(
        Box::from(Expr::Var(Intern::from_ref(&v))),
        Box::from(e),
    ))),
    <v: Var> "%=" <e: Expression> => Expr::VarAssign(Intern::from_ref(&v), Box::from(
    Expr::Mod(
        Box::from(Expr::Var(Intern::from_ref(&v))),
        Box::from(e),
    ))),
    <v: Var> "^=" <e: Expression> => Expr::VarAssign(Intern::from_ref(&v), Box::from(
    Expr::Pow(
        Box::from(Expr::Var(Intern::from_ref(&v))),
        Box::from(e),
    ))),
}

IndexAccess: Expr = {
    <t: Term> <f: ("[" Expression "]")+> => {
        Expr::GetIndex(Box::from(t), f.iter().map(|x| x.1.clone()).collect::<Vec<Expr>>().into_boxed_slice())
    }
}

Expression: Expr = {
    #[precedence(level="0")]
    Term,
    IndexAccess,
    // method call
    <l:Expression> "." <f:FunctionCall> => {
        if let Expr::FunctionCall(x,y) = f {
            return Expr::ObjFunctionCall(Box::from(l),x,y);
        } else {
            unreachable!()
        }
    },

    // OPS
    #[precedence(level="1")] #[assoc(side="left")]
    <l:Expression> "||" <r:Expression> => Expr::BoolOr(Box::from(l),Box::from(r)),

    #[precedence(level="2")] #[assoc(side="left")]
    <l:Expression> "&&" <r:Expression> => Expr::BoolAnd(Box::from(l),Box::from(r)),

    #[precedence(level="3")] #[assoc(side="left")]
    <l:Expression> "==" <r:Expression> => Expr::Eq(Box::from(l),Box::from(r)),
    #[precedence(level="3")] #[assoc(side="left")]
    <l:Expression> "!=" <r:Expression> => Expr::NotEq(Box::from(l),Box::from(r)),

    #[precedence(level="4")] #[assoc(side="left")]
    <l:Expression> "<" <r:Expression> => Expr::Inf(Box::from(l),Box::from(r)),
    #[precedence(level="4")] #[assoc(side="left")]
    <l:Expression> "<=" <r:Expression> => Expr::InfEq(Box::from(l),Box::from(r)),
    #[precedence(level="4")] #[assoc(side="left")]
    <l:Expression> ">" <r:Expression> => Expr::Sup(Box::from(l),Box::from(r)),
    #[precedence(level="4")] #[assoc(side="left")]
    <l:Expression> ">=" <r:Expression> => Expr::SupEq(Box::from(l),Box::from(r)),

    #[precedence(level="5")] #[assoc(side="right")]
    <l:Expression> "^" <r:Expression> => Expr::Pow(Box::from(l),Box::from(r)),

    #[precedence(level="6")] #[assoc(side="left")]
    <l:Expression> "*" <r:Expression> => l * r,
    #[precedence(level="6")] #[assoc(side="left")]
    <l:Expression> "/" <r:Expression> => l / r,
    #[precedence(level="6")] #[assoc(side="left")]
    <l:Expression> "%" <r:Expression> => l % r,

    #[precedence(level="7")] #[assoc(side="left")]
    <l:Expression> "+" <r:Expression> => l + r,
    #[precedence(level="7")] #[assoc(side="left")]
    <l:Expression> "-" <r:Expression> => l - r,
    #[precedence(level="8")] #[assoc(side="left")]
    "-" <r:Expression> => -r,
}

Term: Expr = {
    "(" <e: Expression> ")" => e,
    <t: Num> => Expr::Num(t),
    <b: Bool> => Expr::Bool(b),
    <s: String> => Expr::String(s),
    <v: Var> => Expr::Var(Intern::from(v)),
    <a: Array> => <>,
    <f: FunctionCall> => <>,
    <c: InlineConditionalBlock> => <>,
}

ItemBefore<T,S>: T = {
    <l: T> S => l
}
ItemAfter<S,T>: T = {
    S <l: T> => l
}

FunctionCall: Expr = {
    <namespace: ItemBefore<Var, "::">+?> <o1: Var> <args: Args<Expression>> => {
        Expr::FunctionCall(args.into_boxed_slice(), {
            if let Some(ns) = namespace {
                Box::from([ns.as_slice(), &[o1]].concat())
            } else {
                Box::from(vec![String::from(o1)])
            }
        })
    }
}

Array:Expr = {
    "[" <arr: Comma<Expression>> "]" => Expr::Array(arr.into_boxed_slice())
}

Num: Num = {
    <n: r"([0-9]*[.])?[0-9]+"> => Num::from_str(n).unwrap_or_else(|_| {error_b!(format_args!("Invalid number: '{color_red}{n}{color_reset}'"));})
}

Bool: bool = {
    "true" => true,
    "false" => false,
}

String: String = {
    <s: r#"\"(?:\"\"|[^\"])*\""#> => String::from(s.trim_matches('"'))
}

Var: String = {
    <v: r#"[a-zA-Z_]*"#> => String::from(v),
}
